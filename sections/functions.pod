Z<functions>

X<function>
X<subroutine>

A I<function> (or I<subroutine>, though less often) in Perl is a discrete,
encapsulated unit of behavior.  It may or may not have a name.  It may or may
not take parameters.  It may or may not return arguments.  It represents a type
of control flow, where the execution of the program proceeds to another point
in the source code.

Functions are a prime mechanism for abstraction and encapsulation in Perl 5;
many of the other mechanisms for abstraction, encapsulation, and re-use build
on the concepts of the function.

=head2 Declaring Functions

X<subroutines; declaration>
X<functions; declaration>

Use the C<sub> keyword to declare a function:

=begin programlisting

    B<sub> greet_me  { ... }

=end programlisting

Now C<greet_me()> is available for invocation anywhere else within the
program (with appropriate namespacing or exporting caveats).

=begin sidebar

You do not have to declare Perl 5 functions before you use them, except in the
special case where they modify I<how> the parser parses them.  See
L<attributes>.

=end sidebar

=head2 Invoking Functions

X<subroutines; invoking>
X<functions; invoking>

Invoking a subroutine is similar; mention its name and pass a list of
arguments:

=begin programlisting

    greet_me( 'Jack', 'Brad' );
    greet_me( 'Snowy' );
    greet_me();

=end programlisting

=begin sidebar

You can I<often> omit parameter-grouping parentheses if your program runs
correctly with the C<strict> pragma enabled, but they provide clarity to the
parser and, more importantly, the reader -- so it's often wise to include them.

=end sidebar

You can, of course, pass multiple I<types> of arguments to a function:

=begin programlisting

    greet_me( $name );
    greet_me( @authors );
    greet_me( %editors );

=end programlisting

... though see the L<references> section for more details.

=head2 Function Parameters

X<parameters>
X<functions; parameters>
X<@_>
X<parameter flattening>

Inside the subroutine, all parameters come in a single array, C<@_>.  If C<$_>
corresponds to the English word I<it>, C<@_> corresponds to the word I<them>.
Perl takes care of I<flattening> all incoming parameters into a single list for
you, but the subroutine itself must unpack all parameters into any variables it
wishes to use or operate on C<@_> directly:

=begin programlisting

    sub greet_one
    {
        B<my ($name) = @_>;
        say "Hello, $name!";
    }

    sub greet_all
    {
        say "Hello, B<$_!" for @_>;
    }

=end programlisting

C<@_> behaves as does any other array in Perl.  You may refer to individual
elements by index:

=begin programlisting

    sub greet_one_indexed
    {
        B<my $name = $_[0]>;
        say "Hello, $name!";

        # or, less clear
        say "Hello, $_[0]!";
    }

=end programlisting

You may also C<shift>, C<unshift>, C<push>, C<pop>, C<splice>, and list slices
on C<@_>. Inside a function, the C<shift> and C<pop> operators operate on C<@_>
in the same way that they operate on C<@ARGV> outside of any function:

=begin programlisting

    sub greet_one_shift
    {
        B<my $name = shift>;
        say "Hello, $name!";
    }

=end programlisting

=begin sidebar

It may seem clearer initially to write C<shift @_>, but the C<shift> assignment
idiom is so pervasive in Perl 5 culture that it's important to learn the
shorthand version anyway.

=end sidebar

Do take care that assigning a scalar parameter from C<@_> requires C<shift>,
indexed access to C<@_>, or lvalue list context parentheses.  Otherwise, Perl 5
will happily evaluate C<@_> in scalar context for you and assign the number of
parameters passed:

=begin programlisting

    sub bad_greet_one
    {
        B<my $name = @_>;  # buggy
        say "Hello, $name; you're looking quite numeric today!"
    }

=end programlisting

With more than one parameter, list assignment is often clearer than multiple
lines of C<shift>.  Compare:

=begin programlisting

    sub calculate_value
    {
        # multiple shifts
        my $left_value  = shift;
        my $operation   = shift;
        my $right_value = shift;
        ...
    }

=end programlisting

... to:

=begin programlisting

    sub calculate_value
    {
        B<my ($left_value, $operation, $right_value) = @_;>
        ...
    }

=end programlisting

While occasionally it's necessary to extract a few parameters from C<@_> and
pass the rest on to another function, it's often clearest to use C<shift> to
access a single parameter and list assignment to access multiple parameters.

=head3 Flattening

On the caller side, all arguments get flattened into a single list.  Thus
passing a hash produces a list of key/value pairs:

=begin programlisting

    sub show_pets
    {
        my %pets = @_;
        while (my ($name, $type) = each %pets)
        {
            say "$name is a $type";
        }
    }

    my %pet_names_and_types = (
        Lucky   = > 'dog',
        Rodney  = > 'dog',
        Tuxedo  = > 'cat',
        Petunia = > 'cat',
    );

    show_pets( %pet_names_and_types );

=end programlisting

The C<show_pets()> function works because the C<%pet_names_and_types> hash
flattens into the list C<'Lucky', 'dog', 'Rodney', 'dog', 'Tuxedo', 'cat',
'Petunia', 'cat'>. The hash assignment inside the function C<show_pets()> works
essentially as the more explicit assignment to C<%pet_names_and_types> does.

This is often useful, but there are limitations to understand.  If you wish to
make a C<show_pets_of_type()> function, where one parameter is the I<type> of
pet to display, you must pass that type as the I<first> parameter (or use
C<pop> to remove it from the end of C<@_>):

=begin programlisting

    sub show_pets_by_type
    {
        B<my ($type, %pets) = @_>;

        while (my ($name, $species) = each %pets)
        {
            B<next unless $species eq $type;>
            say "$name is a $species";
        }
    }

    my %pet_names_and_types = (
        Lucky   = > 'dog',
        Rodney  = > 'dog',
        Tuxedo  = > 'cat',
        Petunia = > 'cat',
    );

    show_pets_by_type( 'dog',   %pet_names_and_types );
    show_pets_by_type( 'cat',   %pet_names_and_types );
    show_pets_by_type( 'moose', %pet_names_and_types );

=end programlisting

=head3 Slurping

Z<parameter slurping>
X<parameter slurping>

As with any lvalue assignment to an aggregate, assigning to C<%pets> within the
function I<slurps> all of the remaining values from C<@_>.  If the C<$type>
parameter came at the end of C<@_>, Perl would attempt to assign an odd number
of elements to the hash and would produce a warning.  You I<could> work around
that:

=begin programlisting

    sub show_pets_by_type
    {
        B<my $type = pop;>
        B<my %pets = @_;>

        ...
    }

=end programlisting

... but clarity might suffer.  The same principle applies when assigning to an
array as a parameter, of course.  See L<references> for ways to avoid
flattening and slurping when passing aggregate parameters.

=head3 Aliasing

X<parameter aliasing>
X<functions; aliasing parameters>

One remanining feature of C<@_> can be surprising (though useful): it contains
aliases to the passed-in parameters, at least until you unpack C<@_> into its
own variables.  This behavior is easiest to demonstrate with an example:

=begin programlisting

    sub modify_name
    {
        $_[0] = reverse $_[0];
    }

    my $name = 'Orange';
    modify_name( $name );
    say $name;

    # prints C<egnarO>

=end programlisting

If you modify an element of C<@_> directly, you modify the original parameter
directly.  Be cautious with this.

=begin sidebar

See the C<signatures>, C<Method::Signatures>, and C<MooseX::Method::Signatures>
modules on the CPAN for declarative parameter handling.

=end sidebar

=head3 Validating

Defensive programming often benefits from checking types and values of
arguments for appropriateness before performing further processing.  By
default, Perl 5 provides few built-in mechanisms for doing so (in some cases,
L<prototypes> can I<help>, but that's not their primary purpose). You can
check that the I<number> of parameters passed to a function is correct by
evaluating C<@_> in scalar context:

=begin programlisting

    sub add_numbers
    {
        croak "Expected two numbers, but received: " . @_
            unless @_ == 2;

        ...
    }

=end programlisting

Type checking is more difficult, because of Perl's operator-oriented type
conversions (see L<context>).  In cases where you need more strictness,
consider the CPAN module C<Params::Validate>.

=head2 Namespaces

Every function lives in a namespace.  Functions in an undeclared namespace --
that is, functions not declared after an explicit C<package ...> statement --
live in the C<main> namespace.  You may specify a function's namespace outside
of the current package at the point of declaration:

=begin programlisting

    sub B<Extensions::Math::>add {
        ...
    }

=end programlisting

Any prefix on the function's name which follows the package naming format
creates the subroutine and inserts it into that package.  It does not enter the
current package.  Because Perl 5 packages are open for modification at any
point, you may do this even if the namespace does not yet exist, or if you have
already declared functions in that namespace.

You may only declare one function of the same name per namespace; Perl 5 will
give a warning about subroutine redefinition if you redefine a function which
already exists in a namespace.  If you're certain you want to I<replace> the
existing function, you may disable this warning with C<no warnings 'redefine'>.

You may call functions in other namespaces by using their fully-qualified
names:

=begin programlisting

    package main;

    Extensions::Math::add( $scalar, $vector );

=end programlisting

Functions in namespaces are I<visible> outside of those namespaces in the sense
that you can refer to them directly, but they are only callable by their short
names from within the namespace in which they are declared--unless you have
somehow made them available to the current namespace through the processes of
importing and exporting.

=head3 Importing

X<functions; importing>

When loading a module with the C<use> keyword (see L<modules>), Perl
automatically calls a method named C<import()> on the provided package name.
Modules with procedural interfaces can provide their own C<import()> which
makes some or all defined functions available in the calling package's
namespace.

You don't have to know this to I<import> functions into your namespace.  When
loading a module with the C<use> keyword, any arguments after the name of the
module get passed to the module's C<import()> method.  Thus:

=begin programlisting

    use strict;

=end programlisting

... loads the F<strict.pm> module, calls C<< strict->import() >>, and passes no arguments to the method, while:

=begin programlisting

    use strict 'refs';
    use warnings qw( subs vars );

=end programlisting

... loads the F<warning.pm> module, calls C<< warnings->import( 'refs' ) >>, then calls C<< warnings->import( 'subs', vars' ) >>.

You may call a module's C<import()> method directly.  The previous code example
is equivalent to:

=begin programlisting

    BEGIN
    {
        require strict;
        strict->import( 'refs' );
        strict->import( qw( subs vars ) );
    }

=end programlisting

Be aware that the C<use> keyword adds an implicit C<BEGIN> block around these
statements so that the C<import()> call happens I<immediately> after the parser
has compiled the entire statement.

=for review

Discuss unimporting?  In my experience, it's not nearly as well supported as
importing. (I agree with you, I rarely (okay, never) use it.) (jeremiah)

=end for

=head3 Exporting

=for review

Does this belong instead in the Modules chapter?

+1 (jeremiah)

=end for

X<functions; exporting>

The core C<Exporter> module allows you to accomplish this I<exporting> behavior
without having to understand the details of Perl 5's namespaces and symbol
tables.

=begin programlisting

    package Extensions::Math;

    B<use Exporter 'import';>
    B<our @EXPORT_OK = qw( add subtract multiply divide );>

=end programlisting

Begin by C<use>ing the C<Exporter> module, giving it the single parameter
C<import>.  This tells C<Exporter> to make an C<import()> function available.
The C<@EXPORT_OK> package global is an array which contains a list of functions
to export only when the invoking package requests them.  That is, if someone
writes:

=begin programlisting

    use Extensions::Math 'add';

=end programlisting

... then the function C<Extensions::Math::add()> will be available as C<add()>
in the current namespace, but C<Extensions::Math::divide()> and the others will
only be invokable by their fully-qualified names.

=begin sidebar

The CPAN module C<Sub::Exporter> is significantly more powerful in many ways
than C<Exporter>.  You may prefer its interface.  Be aware that C<Exporter> can
export symbols other than functions; C<Sub::Exporter> only exports functions.

=end sidebar

=head2 Reporting Errors

X<caller>

Within a subroutine, you can get information about the context of the call with
the C<caller> operator.  With no arguments, it returns a three-element list
containing the name of the calling package, the name of the file containing the
call, and the line number of the package on which the call occurred:


=begin programlisting

    package main;

    main();

    sub main
    {
        show_call_information();
    }

    sub show_call_information
    {
        my ($package, $file, $line) = caller();
        say "Called from $package in $file at $line";
    }

=end programlisting

You may pass a single integer argument to C<caller()>.  If you do, the return
values will represent the call that number of calls up the call chain.  You get
more information, including the name of the subroutine, the calling context,
and more.

=begin programlisting

    sub show_call_information
    {
        my ($package, $file, $lineB<, $func>) = caller(B<1>);
        say "Called B<$func> from $package in $file at $line";
    }

=end programlisting

This can be useful.  The standard C<Carp> module uses it to great effect for
reporting errors and throwing warnings in functions.  You may see C<croak()>
used to throw exceptions in place of C<die> in library code.  C<croak()> throws
an exception and reports a stack trace from the perspective of a caller.  The
C<cluck()> function gives a warning from the perspective of a caller.

This behavior is most useful when validating parameters or preconditions of a
function, when you want to indicate that the calling code is wrong somehow:

=begin programlisting

    use Carp 'croak';

    sub add_two_numbers
    {
        croak 'add_two_numbers() takes two and only two arguments'
            unless @_ == 2;

        ...
    }

=end programlisting

=head2 Advanced Functions

Functions may seem simple, but you can do much, much more with them.
L<closures> and L<anonymous_functions> show some of these tricks.  There are
more.

=begin sidebar

I<Higher Order Perl>, by Mark Jason Dominus, is well worth your time studying.
See U<http://hop.perl.plover.com/> for ordering information and the free
version online.

=end sidebar

=head3 Recursion

X<recursion>
X<call frame>
X<functions; call frame>

Every call to a function in Perl creates a new I<call frame>.  This is an
internal data structure which represents the call itself: in effect, incoming
parameters, the point to which to return, and all of the control of the program
leading up to the point of the call.  As well, it captures the lexical
environment of the specific and current invocation of the function.  This means
that a function can I<recur>; it can call itself.

Recursion is a deceptively simple concept, but it can seem daunting if you
haven't encountered it before.  Consider a case where you want to find an
element in a sorted array.  You I<could> iterate through every element of the
array individually, looking for the target, but on average, you'll have to look
at half of the elements of the array at once.

Another approach is to divide the array in half.  Pick the element at the
midpoint, compare, then see if you have to divide the lower half or the upper
half and continue.  You can write this algorithm with a loop yourself, or you
could let Perl manage all of the state and tracking necessary with a recursive
algorithm.  That might look something like:

=begin programlisting

    use Modern::Perl;

    use Test::More tests => 8;

    my @elements = ( 1, 5, 6, 19, 48, 77, 997, 1025, 7777, 8192, 9999 );

    ok   elem_exists(     1, @elements ), 'found first element in array';
    ok   elem_exists(  9999, @elements ), 'found last element in array';
    ok ! elem_exists(   998, @elements ), 'did not find element not in array';
    ok ! elem_exists(    -1, @elements ), 'did not find element not in array';
    ok ! elem_exists( 10000, @elements ), 'did not find element not in array';

    ok   elem_exists(    77, @elements ), 'found midpoint element';
    ok   elem_exists(    48, @elements ), 'found end of lower half element';
    ok   elem_exists(   997, @elements ), 'found start of upper half element';

    sub elem_exists
    {
        my ($item, @array) = @_;

        # break recursion if there are no elements to search
        return unless @array;

        # bias down, if there are an odd number of elements
        my $midpoint = int( (@array / 2) - 0.5 );
        my $miditem  = $array[ $midpoint ];

        # return true if the current element is the target
        return 1 if $item  == $miditem;

        # return false if the current element is the only element
        return   if @array == 1;

        # split the array down and recurse
        return B<elem_exists>( $item, @array[0 .. $midpoint] )
            if $item < $miditem;

        # split the array up and recurse
        return B<elem_exists>( $item, @array[$midpoint + 1 .. $#array] );
    }

=end programlisting

This isn't necessarily the best algorithm for searching a sorted list, but it
demonstrates recursion.  Again, you I<can> write this code in a procedural way;
but some algorithms are much clearer recursively.

=head3 Lexicals

Every new invocation of a function creates its own I<instance> of a lexical
scope.  In the case of the recursive example, even though the declaration of
C<elem_exists(>) creates a single scope for the lexicals C<$item>, C<@array>,
C<$midpoint>, and C<$miditem>, every I<call> to C<elem_exists()>, even
recursively, has separate storage for the values of those lexical variables.
You can demonstrate that by adding debugging code to the function:

=begin programlisting

    B<use Carp 'cluck';>

    sub elem_exists
    {
        my ($item, @array) = @_;

        B<cluck "[$item] (@array)";>

        # other code follows
        ...
    }

=end programlisting

The output demonstrates that not only can C<elem_exists()> call itself safely,
but the lexical variables do not interfere with each other.

=head3 Tail Calls

X<recursion; guard conditions>

One I<drawback> of recursion is that you must get your return conditions
correct, lest your function call itself an infinite number of times.  This is
why the C<elem_exists()> function has several C<return> statements.

Perl offers a helpful warning when it detects what might be runaway recursion,
the C<Deep recursion on subroutine> message.  The limit is 100 recursive calls,
which can be too few in certain circumstances but too many in others.

X<functions; tail call elimination>

Because each call to a function requires a new call frame, as well as space for
the call to store its own lexical values, highly-recursive code can use more
memory than iterative code.  A feature called I<tail call elimination> can
help.

=begin sidebar

To be more specific, tail call elimination may be most obvious when writing
recursive code, but it can be useful in any case of a tail call.

=end sidebar

X<tail calls>
X<functions; tail calls>

A I<tail call> is a call to a function which directly returns that function's
results.  That is, the lines:

=begin programlisting

        # split the array down and recurse
        return elem_exists( $item, @array[0 .. $midpoint] )
            if $item < $miditem;

        # split the array up and recurse
        return elem_exists( $item, @array[$midpoint + 1 .. $#array] );

=end programlisting

... which return the results of the recursive C<elem_exists()> calls directly,
are candidates for tail call elimination.  This elimination avoids returning to
the current call and then returning to the parent call.  Instead, it returns to
the parent call directly.

Perl 5 supports manual tail call elimination, but two recent CPAN modules
provide a much better syntax (and even better performance).  Yuval Kogman's
C<Sub::Call::Tail> and C<Sub::Call::Recur> are worth exploring if you find
yourself with highly recursive code or code that could benefit from tail call
elimination.  C<Sub::Call::Tail> is appropriate for tail calls of non-recursive
code:

=begin programlisting

    use Sub::Call::Tail;

    sub log_and_dispatch
    {
        my ($dispatcher, $request) = @_;
        warn "Dispatching with $dispatcher\n";

        return dispatch( $dispatcher, $request );
    }

=end programlisting

In this example, you can replace the C<return> with the new C<tail> keyword
with no functional changes (yet more clarity and improved performance):

=begin programlisting

        B<tail> dispatch( $dispatcher, $request );

=end programlisting

=for editor

Sadly, S::C::R 0.0.4 caused infinite loops for me on Perl 5.10.0; I'm not sure
if this is Yuval's bug or mine.  I asked him.

=end for

Similarly, C<Sub::Call::Recur> is appropriate for recursive calls.  Replace the C<return elem_exists> code with:

=begin programlisting

        # split the array down and recurse
        B<recur>( $item, @array[0 .. $midpoint] )
            if $item < $miditem;

        # split the array up and recurse
        B<recur>( $item, @array[$midpoint + 1 .. $#array] );

=end programlisting

X<goto>
X<functions; goto>

If you really I<must> perform your own manual tail call elimination, a special
form of the C<goto> keyword exists.  Unlike the form which can often lead to
spaghetti code, the C<goto> function form replaces the current subroutine call
with a call to another subroutine.  You may use a subroutine by name or by
reference.  You must always set C<@_> yourself manually, if you want to pass
different arguments:

=begin programlisting

        # split the array down and recurse
        if ($item < $miditem)
        {
            @_ = ($item, @array[0 .. $midpoint]);
            B<goto &elem_exists;>
        }

        # split the array up and recurse
        else
        {
            @_ = ($item, @array[$midpoint + 1 .. $#array] );
            B<goto &elem_exists;>
        }

=end programlisting

The comparative cleanliness of the CPAN versions should be obvious.

=head2 Pitfalls and Misfeatures

X<functions; pitfalls>
X<functions; misfeatures>

Not all features of Perl 5 functions are always helpful.  In particular,
L<prototypes> rarely do what you mean.  They have their uses, but you can avoid
them outside of a few cases.

=for editor

Check the timeline, but I'm pretty sure about this.

=end for

X<functions; Perl 4>
X<functions; Perl 1>

As well, Perl 5 still supports old-style invocations of functions, carried over
from older versions of Perl.  While you may now invoke Perl functions by name,
previous versions of Perl required you to invoke them with a leading ampersand
(C<&>) character.  Perl 1 required you to use the C<do> keyword as well:

=begin programlisting

    # Perl 4 style; avoid
    my $result = &calculate_result( 52 );

    # Perl 1 style; really truly avoid
    my $result = do &calculate_result( 42 );

=end programlisting

While they're visually noisy, with vestigial syntax, the leading ampersand form
performs other behavior which can occasionally surprise you.  First, it
disables prototype checking (but prototypes are a pitfall often best avoided).
Second, if you do not pass arguments explicitly, it I<implicitly> passes the
contents of C<@_> unmodified.  Both can lead to surprising behavior.

A final pitfall comes from leaving the parentheses off of function calls.  The
Perl 5 parser uses several heuristics to resolve ambiguity of barewords and the
number of parameters passed to a function, but occasionally those heuristics
guess wrong.  While it's often wise to remove extraneous parentheses, compare
the readability of these two lines of code:

=begin programlisting

    ok( elem_exists( 1, @elements ), 'found first element in array' );

    # warning; contains a subtle bug
    ok elem_exists 1, @elements, 'found first element in array';

=end programlisting

The subtle bug in the second form is that the call to C<elem_exists()> will
gobble up the test description intended as the second argument to C<ok()>.
Because C<elem_exists()> uses a slurpy second parameter, this may go unnoticed
until Perl produces warnings about comparing a non-number (the test
description, which it cannot convert into a number) with the element in the
array.

This is admittedly an extreme case, but it is a case where proper
parenthesization can clarify code and make subtle bugs obvious to the reader.

=for author

Add C<wantarray()> and Want.pm information?

=end for
